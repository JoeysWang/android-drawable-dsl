buildscript {
  System.setProperty("java.awt.headless", "true") // mergeDebugResources spawns annoying AWT UI

  ext {
    versions = [
        android       : "3.0+",
        kotlin        : "1.1.51",
        gradleVersions: "0.17.0",
        supportTest   : "1.0.1",
        anko          : "0.10.2",
    ]
  }

  repositories {
    jcenter()
    google()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:$versions.android"
    classpath "com.github.ben-manes:gradle-versions-plugin:$versions.gradleVersions"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
  }
}

apply plugin: "com.github.ben-manes.versions"
apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
  compileSdkVersion 26

  defaultConfig {
    minSdkVersion 12
    targetSdkVersion 26

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  sourceSets.each {
    it.java.srcDirs += "src/${it.name}/kotlin"
  }
}

repositories {
  jcenter()
  google()
}

configurations {
  ktlint
}

dependencies {
  ktlint "com.github.shyiko:ktlint:0.11.1"

  implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"

  androidTestImplementation "com.android.support.test:runner:$versions.supportTest"
  androidTestImplementation "io.kotlintest:kotlintest:2.0.7"
  androidTestImplementation "org.jetbrains.anko:anko-commons:$versions.anko"
}

task ktlint(type: JavaExec, group: "verification") {
  description = "Check Kotlin code style."
  main = "com.github.shyiko.ktlint.Main"
  classpath = configurations.ktlint
  args "src/**/*.kt"
  // to generate report in checkstyle format prepend following args:
  // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
  // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
  description = "Fix Kotlin code style deviations."
  main = "com.github.shyiko.ktlint.Main"
  classpath = configurations.ktlint
  args "-F", "src/**/*.kt"
}
